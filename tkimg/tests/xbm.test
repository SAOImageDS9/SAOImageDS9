# This file is a Tcl script to test XBM reading and writing.
# It is organized in the standard fashion for Tcl tests.

package require Tk

package require tcltest
tcltest::configure {*}$argv
source [file join [file dirname [info script]] constraints.tcl]

imageInit

namespace eval ::xbm::test {
    namespace import ::tcltest::*

    set fmt     "xbm"
    set ext     "xbm"
    set inFile  "testimgs/img.$ext"
    set outFile "testimgs/img_out.$ext"

    set imgdataFile [readFile $inFile]
    # XBM images directly written to a file have the file name as
    # name of the static char variable: static char img_bits[] =
    # XBM images written to a string (as used in the following tests)
    # use "InlineData" as variable name.
    # Substitute "img" with "InlineData" for textual comparison.
    set imgdata [string map {"img" "InlineData"} $imgdataFile]

    test xbm-0.1 {package require img::xbm} -setup {
    } -body {
        set pkgLoaded [catch {package require img::xbm}]
    } -result 0

    # Execute the follwing tests only, if the format handler could be loaded.
    if { $pkgLoaded == 0 } {
        test xbm-1.1 {Read photo from file using option \"-file\"} -setup {
            catch {image delete i}
        } -body {
            image create photo i -file $inFile
            i data -format $fmt
        } -cleanup {
            image delete i
        } -result $imgdata

        test xbm-1.2 {Read photo from string using option \"-data\"} -setup {
            catch {image delete i}
        } -body {
            image create photo i -data $imgdata
            i data -format $fmt
        } -cleanup {
            image delete i
        } -result $imgdata

        test xbm-1.3 {Read photo from string using command \"put\"} -setup {
            catch {image delete i}
        } -body {
            image create photo i
            i put $imgdata
            i data -format $fmt
        } -cleanup {
            image delete i
        } -result $imgdata

        test xbm-1.4 {Read photo from string using command \"put -format\"} -setup {
            catch {image delete i}
        } -body {
            image create photo i
            i put $imgdata -format $fmt
            i data -format $fmt
        } -cleanup {
            image delete i
        } -result $imgdata

        test xbm-1.5 {Compare 2 photos read from file and from string} -setup {
            catch {image delete i}
        } -body {
            image create photo i -file $inFile
            image create photo j -data $imgdata
            imageCompare i j
        } -cleanup {
            image delete i
            image delete j
        } -result 1


        test xbm-2.1 {Read photo from file without resolution information} -setup {
            catch {image delete i}
        } -body {
            image create photo i -file $inFile
            imageResolution i
        } -cleanup {
            image delete i
        } -constraints {
            Tk87
        } -result {0 0}


        test xbm-3.1 {Write photo to file and compare with reference photo} -setup {
            catch {image delete i}
        } -body {
            image create photo i -file $inFile
            i write $outFile -format [list $fmt]
            image create photo j -file $outFile
            imageCompare i j
        } -cleanup {
            image delete i
            image delete j
            file delete -force $outFile
        } -constraints {
            Tk87
        } -result 1

        # Write photo to string using "i data -format" already tested in xbm-1.X tests.

        test xbm-5.1 {Read photo from file using invalid option \"-invalidoption\"} -setup {
            catch {image delete i}
        } -body {
            image create photo i -file $inFile -format [list $fmt -invalidoption 1]
        } -returnCodes {
            error
        } -result "bad format option \"-invalidoption\": must be -verbose, -background, or -foreground"

        test xbm-5.2 {Read photo from file using option without value} -setup {
            catch {image delete i}
        } -body {
            image create photo i -file $inFile -format [list $fmt -verbose]
        } -returnCodes {
            error
        } -result "No value specified for option \"-verbose\"."

        test xbm-5.3 {Read photo from file using invalid -verbose value} -setup {
            catch {image delete i}
        } -body {
            image create photo i -file $inFile -format [list $fmt -verbose "asdf"]
        } -returnCodes {
            error
        } -result "Invalid verbose mode \"asdf\": must be 1 or 0, on or off, true or false."

        test xbm-5.4 {Read photo from file using option without value} -setup {
            catch {image delete i}
        } -body {
            image create photo i -file $inFile -format [list $fmt -foreground]
        } -returnCodes {
            error
        } -result "No value specified for option \"-foreground\"."

        test xbm-5.5 {Read photo from file using invalid -foreground value} -setup {
            catch {image delete i}
        } -body {
            image create photo i -file $inFile -format [list $fmt -foreground "asdf"]
        } -returnCodes {
            error
        } -result "unknown color name \"asdf\""

        test xbm-5.6 {Read photo from file using option without value} -setup {
            catch {image delete i}
        } -body {
            image create photo i -file $inFile -format [list $fmt -background]
        } -returnCodes {
            error
        } -result "No value specified for option \"-background\"."

        test xbm-5.7 {Read photo from file using invalid -background value} -setup {
            catch {image delete i}
        } -body {
            image create photo i -file $inFile -format [list $fmt -background "asdf"]
        } -returnCodes {
            error
        } -result "unknown color name \"asdf\""

        test xbm-5.7 {Write photo to file using invalid option \"-invalidoption\"} -setup {
            catch {image delete i}
        } -body {
            image create photo i -file $inFile
            i write $outFile -format [list $fmt -invalidoption 1]
        } -cleanup {
            image delete i
            file delete -force $outFile
        } -returnCodes {
            error
        } -result "bad format option \"-invalidoption\": must be -verbose"

        test xbm-5.8 {Write photo to file using option without value} -setup {
            catch {image delete i}
        } -body {
            image create photo i -file $inFile
            i write $outFile -format [list $fmt -verbose]
        } -cleanup {
            image delete i
            file delete -force $outFile
        } -returnCodes {
            error
        } -result "No value specified for option \"-verbose\"."

        test xbm-5.9 {Write photo to file using invalid -verbose value} -setup {
            catch {image delete i}
        } -body {
            image create photo i -file $inFile
            i write $outFile -format [list $fmt -verbose "asdf"]
        } -cleanup {
            image delete i
            file delete -force $outFile
        } -returnCodes {
            error
        } -result "Invalid verbose mode \"asdf\": must be 1 or 0, on or off, true or false."
    }
}

imageFinish
tcltest::cleanupTests
namespace delete ::xbm::test
